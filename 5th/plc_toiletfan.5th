
( ***************************************************
  PLC program to control a toilet light and fan.

  GPIO in:  bit 0 is a pushbuttom
  GPIO out: bit 0 is light
          : bit 1 is fan

  - press button to turn light on or off
  - hold button for >0.5s to turn fan on
  - light turns off after 10 minutes max
  - fan turns off 5 mins after light
  *************************************************** )

INCLUDE 5th/plc.5th

( prev GPIO state, for detecting rising/falling edge )
VARIABLE PREVINPUT
( when was the light turned on? )
VARIABLE LIGHTON
( when was the light turned off? )
VARIABLE LIGHTOFF
( when did the button-press start? )
VARIABLE PRESSDOWN
( output state we will use )
VARIABLE STATE

( port and bit definitions )
0 DEFINE INPORT
1 DEFINE BUTTON
0 DEFINE OUTPORT
1 DEFINE LIGHT
2 DEFINE FAN

( timing )
1000    DEFINE PERIOD
500     DEFINE LONGPRESS
5 60 *  DEFINE FANTIMEOUT
10 60 * DEFINE LIGHTTIMEOUT

( set output bits )
: WRITEOUT
  STATE OUTPORT GPIO_WRITE
;

( on button down )
: RISING
  TIME_MSBOOT TO PRESSDOWN      ( remember when press started )
  STATE LIGHT ^ DUP TO STATE    ( toggle light )

  LIGHT & IF
    TIME_EPOCH TO LIGHTON       ( remember when we turned it on/off )
  ELSE
    TIME_EPOCH TO LIGHTOFF
  ENDIF

  WRITEOUT
;

( on button release )
: FALLING
  TIME_MSBOOT PRESSDOWN -       ( duration of press )
  LONGPRESS > IF
    STATE FAN | TO STATE        ( turn fan on )
    WRITEOUT
  ENDIF
;

( for GPIO change events )
: ONCHANGE
  INPORT GPIO_READ
  DUP PREVINPUT ~ & BUTTON &    ( rising edge? )
  IF
    RISING
  ENDIF
  DUP ~ PREVINPUT & BUTTON &    ( falling edge? )
  IF
    FALLING
  ENDIF
  TO PREVINPUT          ( save input state )  
;

( timer event, check for timeouts)
: ONTIMER
  STATE LIGHT & IF
    ( light is on, see if it needs to turn off )
    TIME_EPOCH LIGHTON - LIGHTTIMEOUT >
    IF
      TIME_EPOCH TO LIGHTOFF
      STATE LIGHT ^ TO STATE
      WRITEOUT
    ENDIF
  ELSE
    STATE FAN & IF
      ( light is off and fan is on, see if it needs to turn off )
      TIME_EPOCH LIGHTOFF - FANTIMEOUT >
      IF
        STATE FAN ^ TO STATE
        WRITEOUT
      ENDIF
    ENDIF
  ENDIF
;

( boot: initialise state and install event handlers )
: MAIN
  0 TO STATE                         ( clear outputs )
  WRITEOUT  
  INPORT ' ONCHANGE GPIO_HANDLER     ( install GPIO-change handler )
  PERIOD ' ONTIMER TIMER_PERIODIC    ( start timer )
;

( GC/save code to a binary )
DUMP
[FUNCPTR] MAIN GC
